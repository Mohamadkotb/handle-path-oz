/*
 * Created by Murillo Comino on 26/07/20 00:04
 * Github: github.com/onimur
 * StackOverFlow: pt.stackoverflow.com/users/128573
 * Email: murillo_comino@hotmail.com
 *
 *  Copyright (c) 2020.
 *  Last modified 25/07/20 23:51
 */

apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def applicationName = "HandlePathOZ"

buildscript {

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.triplet.gradle:play-publisher:2.8.0'
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId 'br.com.onimur.sample.handlepathoz'
        minSdkVersion 16
        targetSdkVersion 29
        versionCode = libVersionCode
        versionName = libVersionName
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            Properties secret = new Properties()
            File secretFile = project.rootProject.file('secret.properties')
            //for circleCi
            if (secretFile.exists()) {
                secret.load(secretFile.newDataInputStream())
            }

            storeFile file(System.getenv("KEYSTORE") ?: "../.exclude/key/handlepathoz.jks")
            storePassword secret.getOrDefault('keystorePassword', System.getenv("KEYSTORE_PASSWORD"))
            keyAlias secret.getOrDefault('keyAlias', System.getenv("KEY_ALIAS"))
            keyPassword secret.getOrDefault('keyPassword', System.getenv("KEY_PASSWORD"))
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${applicationName}.apk"
                }
            }
        }
    }
    buildToolsVersion = '29.0.3'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

play {
    // see: https://github.com/Triple-T/gradle-play-publisher
    serviceAccountCredentials = file(System.getenv("PUBLISH_GOOGLE") ?: "../.exclude/key/google-play.json")
    track = "production"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation project(path: ':handle-path-oz')

    //Support Libs
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    //Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.7'

    //Material
    implementation 'com.google.android.material:material:1.1.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
